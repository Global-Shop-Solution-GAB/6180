Program.Sub.ScreenSU.Start
Gui.f_Main..Create(BaseForm)
Gui.f_Main..Caption("Salesperson Email Maintenance")
Gui.f_Main..Size(558,620)
Gui.f_Main..MinX(0)
Gui.f_Main..MinY(0)
Gui.f_Main..Position(0,0)
Gui.f_Main..AlwaysOnTop(False)
Gui.f_Main..FontName("Tahoma")
Gui.f_Main..FontSize(8.25)
Gui.f_Main..ControlBox(True)
Gui.f_Main..MaxButton(False)
Gui.f_Main..MinButton(True)
Gui.f_Main..MousePointer(0)
Gui.f_Main..Moveable(True)
Gui.f_Main..Sizeable(True)
Gui.f_Main..ShowInTaskBar(True)
Gui.f_Main..TitleBar(True)
Gui.f_Main..Event(UnLoad,f_Main_UnLoad)
Gui.f_Main.cmdSave.Create(Button)
Gui.f_Main.cmdSave.Enabled(True)
Gui.f_Main.cmdSave.Visible(True)
Gui.f_Main.cmdSave.Zorder(0)
Gui.f_Main.cmdSave.Size(75,23)
Gui.f_Main.cmdSave.Position(474,9)
Gui.f_Main.cmdSave.Caption("Save")
Gui.f_Main.cmdSave.FontName("Tahoma")
Gui.f_Main.cmdSave.FontSize(8.25)
Gui.f_Main.cmdSave.Event(Click,cmdSave_Click)
Gui.f_Main.cmdSave.Anchor(9)
Gui.f_Main.GsGcEmail.Create(GsGridControl)
Gui.f_Main.GsGcEmail.Enabled(True)
Gui.f_Main.GsGcEmail.Visible(True)
Gui.f_Main.GsGcEmail.Zorder(0)
Gui.f_Main.GsGcEmail.Size(558,547)
Gui.f_Main.GsGcEmail.Position(0,43)
Gui.f_Main.GsGcEmail.Dock(2)
Gui.f_Main.GsGcEmail.Anchor(15)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
V.Global.bError.Declare(Boolean)
V.Global.bUnattended.Declare(Boolean)
Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.Try
Function.Intrinsic.UI.UsePixels ' Allows you to use Pixels instead of Twips throughout

'open the connection to the db 
F.ODBC.Connection!con.OpenConnection(V.Ambient.PDSN, V.Ambient.PUser, v.Ambient.PPass)

F.Intrinsic.Control.CallSub(LoadGrid)
F.Intrinsic.Control.CallSub(FormatGrid)

Gui.f_Main..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMessage, "ErrorNumber", V.Ambient.ErrorNumber, "ErrorDescription", V.Ambient.ErrorDescription, "Subroutine", V.Ambient.CurrentSubroutine, "UNATTENDED", V.Global.bUnattended, "Project", "6180", "ProjectTitle", "Salesperson Email Maintenance", "Callback", "f_Main_Unload")
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.LoadGrid.Start
F.Intrinsic.Control.Try
V.Local.sSQL.Declare(String)

F.Intrinsic.Control.If(V.DataTable.dtSalespersonEmail.Exists)
	F.Data.DataTable.Close("dtSalespersonEmail")
F.Intrinsic.Control.EndIf

F.Data.DataTable.CreateFromSQL("dtSalespersonEmail", "con", "SELECT ID, RTRIM(LTRIM(NAME)) AS NAME, '' AS EMAIL FROM V_SALESPERSONS ORDER BY ID", True)

F.Data.Dictionary.CreateFromSQL("dictEmails", "con", "SELECT ID, SALESPERSON_EMAIL AS EMAIL FROM GCG_6180_EMAILMAINT")
F.Data.Dictionary.SetDefaultReturn("dictEmails", "")
F.Data.DataTable.FillFromDictionary("dtSalespersonEmail", "dictEmails", "ID", "EMAIL")
F.Data.Dictionary.Close("dictEmails")

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMessage, "ErrorNumber", V.Ambient.ErrorNumber, "ErrorDescription", V.Ambient.ErrorDescription, "Subroutine", V.Ambient.CurrentSubroutine, "UNATTENDED", V.Global.bUnattended, "Project", "6180", "ProjectTitle", "Salesperson Email Maintenance", "Callback", "f_Main_Unload")
F.Intrinsic.Control.EndTry
Program.Sub.LoadGrid.End

Program.Sub.FormatGrid.Start
F.Intrinsic.Control.Try
V.Local.sDefault.Declare(String)
V.Local.sGV.Declare(String)
V.Local.sProperty.Declare(String)


Gui.f_Main.GsGcEmail.AddGridviewFromDatatable("gvEmails", "dtSalespersonEmail")
V.Local.sGV.Set("gvEmails")

Gui.f_Main.GsGcEmail.SetGridviewProperty(V.Local.sGV, "AllowSort", True)
Gui.f_Main.GsGcEmail.SetGridviewProperty(V.Local.sGV, "AllowFilter", True)
Gui.f_Main.GsGcEmail.SetGridviewProperty(V.Local.sGV, "OptionsViewShowGroupPanel", False)
Gui.f_Main.GsGcEmail.SetGridviewProperty(V.Local.sGV,  "OptionsViewColumnAutoWidth", True)

Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "ID", "Visible", False)

V.Local.sProperty.Set("Caption")
V.Local.sDefault.Set(" ")
Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "NAME", V.Local.sProperty, "Salesperson")
Gui.f_Main.GsGcEmail.SetColumnProperty(V.Local.sGV, "EMAIL", v.Local.sProperty, "Email")

V.Local.sProperty.Set("HeaderHAlignment")
V.Local.sDefault.Set("Center")
Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "NAME", V.Local.sProperty, V.Local.sDefault)
Gui.f_Main.GsGcEmail.SetColumnProperty(V.Local.sGV, "EMAIL", v.Local.sProperty, V.Local.sDefault)

V.Local.sProperty.Set("HeaderFontBold")
V.Local.sDefault.Set("True")
Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "NAME", V.Local.sProperty, V.Local.sDefault)
Gui.f_Main.GsGcEmail.SetColumnProperty(V.Local.sGV, "EMAIL", v.Local.sProperty, V.Local.sDefault)

V.Local.sProperty.Set("AllowEdit")
V.Local.sDefault.Set("False")
Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "NAME", V.Local.sProperty, V.Local.sDefault)
Gui.f_Main.GsGcEmail.SetColumnProperty(V.Local.sGV, "EMAIL", v.Local.sProperty, "True")

V.Local.sProperty.Set("ReadOnly")
V.Local.sDefault.Set("True")
Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "NAME", V.Local.sProperty, V.Local.sDefault)
Gui.f_Main.GsGcEmail.SetColumnProperty(V.Local.sGV, "EMAIL", v.Local.sProperty, "False")

V.Local.sProperty.Set("MinWidth")
V.Local.sDefault.Set("150")
Gui.f_Main.GsGcEmail.SetColumnProperty(v.Local.sGV, "NAME", V.Local.sProperty, V.Local.sDefault)
Gui.f_Main.GsGcEmail.SetColumnProperty(V.Local.sGV, "EMAIL", v.Local.sProperty, 200)

Gui.f_Main.GsGcEmail.MainView(V.Local.sGV)
F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMessage, "ErrorNumber", V.Ambient.ErrorNumber, "ErrorDescription", V.Ambient.ErrorDescription, "Subroutine", V.Ambient.CurrentSubroutine, "UNATTENDED", V.Global.bUnattended, "Project", "6180", "ProjectTitle", "Salesperson Email Maintenance", "Callback", "f_Main_Unload")
F.Intrinsic.Control.EndTry
Program.Sub.FormatGrid.End

Program.Sub.cmdSave_Click.Start
F.Intrinsic.Control.Try

F.Data.DataTable.SaveToDB("dtSalespersonEmail", "con", "GCG_6180_EMAILMAINT", "ID", 256,"ID@!@ID*!*NAME@!@SALESPERSON*!*EMAIL@!@SALESPERSON_EMAIL")
F.Intrinsic.UI.Msgbox("Emails Saved!", "Success")
F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMessage, "ErrorNumber", V.Ambient.ErrorNumber, "ErrorDescription", V.Ambient.ErrorDescription, "Subroutine", V.Ambient.CurrentSubroutine, "UNATTENDED", V.Global.bUnattended, "Project", "6180", "ProjectTitle", "Salesperson Email Maintenance", "Callback", "f_Main_Unload")
F.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.f_Main_UnLoad.Start
F.Intrinsic.Control.Try

'close the connection
F.ODBC.Connection!con.Close

'end the script
F.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMessage, "ErrorNumber", V.Ambient.ErrorNumber, "ErrorDescription", V.Ambient.ErrorDescription, "Subroutine", V.Ambient.CurrentSubroutine, "UNATTENDED", V.Global.bUnattended, "Project", "6180", "ProjectTitle", "Salesperson Email Maintenance", "Callback", "f_Main_Unload")
F.Intrinsic.Control.EndTry
Program.Sub.f_Main_UnLoad.End

Program.Sub.ErrorMessage.Start
'basic error message for replacing catch block
'this is really just for when printing the message and ending program
'pass error number, error description, current subroutine
'optional callback sub, this will be called before ending
'optional unattended parameters, those subs may not exist if not using them but won't be called
F.Intrinsic.Control.Try

V.Local.iError.Declare(Long)
V.Local.sErrorDescription.Declare(String)
V.Local.sSubroutine.Declare(String)
V.Local.sError.Declare(String)
V.Local.bExists.Declare(Boolean)
V.Local.sCallback.Declare(String)

V.Local.iError.Set(V.Args.ErrorNumber)
V.Local.sErrorDescription.Set(V.Args.ErrorDescription)
V.Local.sSubroutine.Set(V.Args.Subroutine)

'make sure we aren't in an error loop
F.Intrinsic.Control.If(V.Global.bError)
	F.Intrinsic.Control.End

F.Intrinsic.Control.EndIf
V.Global.bError.Set(True)

F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}", V.Caller.ScriptFile, V.Ambient.Newline, V.Local.sSubroutine, V.Local.iError, V.Local.sErrorDescription, V.Ambient.GABVersion, V.Local.sError)

F.Intrinsic.Variable.ArgExists("UNATTENDED", V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists)
	F.Intrinsic.Control.If(V.Args.UNATTENDED)
		F.Intrinsic.Control.CallSub(UnattendedErrorMessage, "MESSAGE", V.Local.sError, "PROJECT", V.Args.PROJECT, "PROJECTTITLE", V.Args.PROJECTTITLE)
	F.Intrinsic.Control.Else
		F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Else
	F.Intrinsic.UI.Msgbox(V.Local.sError)
F.Intrinsic.Control.EndIf

F.Intrinsic.Variable.ArgExists("Callback", V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists)
	V.Local.sCallback.Set(V.Args.Callback.Trim)
	F.Intrinsic.Control.CallSub(V.Local.sCallback)
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End

F.Intrinsic.Control.EndTry
Program.Sub.ErrorMessage.End

Program.Sub.Comments.Start
${$5$}$3.0.0.0$}$1
${$6$}$acollins$}$20210330153200227$}$h98hERc2yV72q0GaUGlwSaoK/aefTdVkB5SGxLdVwE9LppyqYPY4w6s9wnp3VQ/i54BoOzC7XWYnSZHjq0shsA==
Program.Sub.Comments.End